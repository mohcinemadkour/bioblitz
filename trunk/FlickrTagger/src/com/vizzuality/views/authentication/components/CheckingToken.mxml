<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	height="175" width="150" borderStyle="solid" borderThickness="0" cornerRadius="0" horizontalScrollPolicy="off" verticalScrollPolicy="off"
	backgroundAlpha="0" creationComplete="onCreationComplete()">
	
	<mx:states>
		<mx:State name="httpError">
			<mx:RemoveChild target="{bar}"/>
			<mx:AddChild position="lastChild">
				<mx:Button x="10" y="38" label="Try again" click="initialState()"/>
			</mx:AddChild>
			<mx:AddChild position="lastChild">
				<mx:Button x="252" y="38" label="Exit" click="NativeApplication.nativeApplication.exit()"/>
			</mx:AddChild>
			<mx:AddChild position="lastChild">
				<mx:Label x="26" y="10" text="Ups!! You aren't connect currently to internet" width="258" textAlign="center"/>
			</mx:AddChild>
			<mx:SetProperty name="width" value="310"/>
			<mx:SetProperty name="height" value="186"/>
		</mx:State>
	</mx:states>
		
	<mx:Script>
		<![CDATA[
			import com.adobe.crypto.MD5;
			import com.adobe.serialization.json.JSON;
			import com.vizzuality.dao.*;
			import com.vizzuality.event.GetTokenEvent;
			import com.vizzuality.views.authentication.FlickrAuthorizationSettings;
			
			import flash.events.Event;
			
			import mx.collections.ArrayCollection;
			import mx.core.Application;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.http.HTTPService;
			
	
			private var sqlArray: ArrayCollection;
			private var object: Object;
			private var ko:GetTokenEvent = new GetTokenEvent(GetTokenEvent.KO_TOKEN);
			private var ok:GetTokenEvent = new GetTokenEvent(GetTokenEvent.OK_TOKEN);

			
			private function onCreationComplete():void {
				createSQL();
				createDirectory();
				askToken();
			}
			
	
			private function askToken():void {
				bar.text="Checking token..."			
				if (FlickrAuthorizationSettings.authToken!=""){
					bar.text="Checking token..."
					var sig:String = FlickrAuthorizationSettings.flickrAPISecret + "api_key" + 
						FlickrAuthorizationSettings.flickrAPIKey + "auth_token" + 
						FlickrAuthorizationSettings.authToken + "formatjsonmethodflickr.auth.checkTokennojsoncallback1";
					var jsonService: HTTPService = new HTTPService();
					jsonService.method = "get";
					jsonService.resultFormat = "text";
					jsonService.addEventListener(ResultEvent.RESULT,onResult);
					jsonService.addEventListener(FaultEvent.FAULT,onFault);
					var flickrurl:String = "http://api.flickr.com/services/rest/?method=flickr.auth.checkToken&format=json&nojsoncallback=1&api_key="+ 
						FlickrAuthorizationSettings.flickrAPIKey + "&auth_token="+
						FlickrAuthorizationSettings.authToken+"&api_sig="+MD5.hash(sig);
					jsonService.url=flickrurl;
			 		jsonService.send();
			 	} else {
	        		dispatchEvent(ko);
	        		trace("Empty Token");
			 	}
				
			}
		
	
			private function onResult(ev:ResultEvent):void{
		        var data:String = String(ev.result);
				try {
			 		object = JSON.decode(data);
				} catch(e:Error) {
					currentState="httpError";
					return;
				}
				
				
				if (object==null || object.stat!="ok" ) {
	        		dispatchEvent(ko);
	        		trace("Token expired");
	 			}
				else {
	        		dispatchEvent(ok);
	        		trace("Token ok");
				}
			}
	
			private function onFault(ev:FaultEvent):void {
	        	currentState='httpError';
			}
			
			private function createDirectory():void {
				bar.text="Creating directories..."
				var dir:File = File.applicationStorageDirectory.resolvePath("images");
				 if ( !dir.exists ) {
				    dir.createDirectory();
				    trace( "Directory created." );
				 }
				 else {
				    trace( "Directory already exists." );
				 }
		
			}
			
			private function createSQL():void {		
				var dao: DataAccessObject = new DataAccessObject();
				dao.createTables(); 

				bar.text="Creating tables..."
				
				dao.openConnection("SELECT COUNT(alias) FROM user");
				sqlArray = dao.dbResult;
			    Application.application.count = dao.countHandler(sqlArray);
			    
			    if (Application.application.count == 1) {
				    dao.openConnection("SELECT alias FROM user");
					sqlArray = dao.dbResult;
		    		FlickrAuthorizationSettings.accountName = sqlArray[0].alias;
				    
					dao.openConnection("SELECT token FROM user");	
					sqlArray = dao.dbResult;
					if (sqlArray.length!=0) {FlickrAuthorizationSettings.authToken = sqlArray[0].token};  
			    }
			}
			
			private function initialState():void {
				currentState='';
				askToken();
			}
		]]>
	</mx:Script>
	<mx:Metadata>
		    [Event(name="onGetTokenOK", type="com.vizzuality.event.GetTokenEvent")]
		    [Event(name="onGetTokenKO", type="com.vizzuality.event.GetTokenEvent")]
	</mx:Metadata>
	<mx:Image source="@Embed('com/vizzuality/assets/loading.png')" horizontalCenter="0" y="10" id="loading_image"/>
	<mx:Label id="bar" text="Starting engines..." y="142" color="#454545" fontSize="15" height="23" horizontalCenter="0" fontWeight="bold" fontFamily="Arial"/>
</mx:Canvas>